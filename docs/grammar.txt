// PROGRAM AND STATEMENT
<program> := <statement>*

<statement> := <declaration>
            | <assignment>

// DECLARATION
<declaration> := <id_declaration>
                | <user_type_declaration>
                | <function_declaration>

<id_declaration> := <data_type> identifier ('=' <expression>)? (',' identifier ('=' <expression>)?)* ';'

<user_type_declaration> := 'define' user_data_type <block> ';'

<function_declaration> := <data_type> function_name '(' args ')' <block>

<block> := '{' <statement>* '}'

args := (<data_type> identifier (',' <data_type> identifier)*)*

// TYPES
<data_type> := <type_keyword>
            | user_defined_type

<type_keyword> := int
                | float
                | char
                | string

// ASSIGNMENT
<assignment> := identifier '=' <expression> ';'

// EXPRESSION
<expression> := <equality>

<equality> := <comparison> (('==' | '!=') <comparison>)*

<comparison> := <term> (('<' | '<=' | '>' | '>=') <term>)*

<term> := <factor> (('+' | '-') <factor>)*

<factor> := <unary> (('*' | '/') <unary>)*

<unary> := ( '-' | '!') <unary>
        | <primary>

<primary> := int_literal
            | float_literal
            | char_literal
            | string_literal
            | user_defined_literal
            | identifier
            | 'true'
            | 'false'
            | 'NULL'
            | '(' <expression> ')'
